    managed;
    strict ( 2 );
    with draft;

    define behavior for ZCDS_R_HEADER_0098 alias Header
    implementation in class zbp_r_header_0098 unique
    draft table zheader_0098_d
    persistent table zheader_0098
    lock master
    total etag LastChangedAt
    authorization master ( instance, global )
    etag master LocalLastChangedAt
    {

      association _Items { create ( features : instance, authorization : update ); with draft; }

      create ( authorization : global, precheck );
      update ( precheck );
      delete ( precheck );

      field ( readonly, numbering : managed ) HeaderUUID;
      field ( readonly ) HeaderId, ItemId, LocalCreatedBy, LocalCreatedAt, LocalLastChangedBy, LocalLastChangedAt, LastChangedAt, OrderStatus;
      field ( mandatory ) Email;

      determination SetHeaderID on save { create; field HeaderId; }
      determination SetStatusOpen on save { create; field OrderStatus; }

      action ( features : instance, authorization : update ) ApproveOrder result [1] $self;
      action ( features : instance, authorization : update ) RejectOrder result [1] $self;

      validation ValidateEmail on save { create; update; }
      validation ValidateName on save { create; update; field FirstName, LastName; }
      validation ValidateDeliveryDate on save { create; update; field DeliveryDate; }
      validation ValidateCountry on save { create; update; field Country; }
      validation ValidateOrderStatus on save { create; update; field OrderStatus; }

      determine action ValidateMail { validation ValidateEmail; }
      determine action ValidateNames { validation ValidateName; }
      determine action ValidateDeliveryDates { validation ValidateDeliveryDate; }
      determine action ValidateCountrys { validation ValidateCountry; }
      determine action ValidateOrderStatuss { validation ValidateOrderStatus; }

      side effects
      {
        field DeliveryDate affects field OrderStatus;
        field Country affects field OrderStatus;

        determine action ValidateMail executed on field Email affects messages;
        determine action ValidateNames executed on field FirstName, field LastName affects messages;
        determine action ValidateDeliveryDates executed on field DeliveryDate affects messages;
        determine action ValidateCountrys executed on field Country affects messages;
        determine action ValidateOrderStatuss executed on field OrderStatus affects messages;
      }

      draft action Activate optimized;
      draft action Edit;
      draft action Resume;
      draft action Discard;

      draft determine action Prepare
      {
        validation ValidateEmail;
        validation ValidateName;
        validation ValidateDeliveryDate;
        validation ValidateCountry;
        validation ValidateOrderStatus;
        validation Item~ValidatePrice;
        validation Item~validateCurrency;
        validation Item~ValidateQuantity;
      }

      mapping for zheader_0098
        {
          HeaderUUID         = id;
          HeaderId           = header_id;
          ItemId             = item_id;
          Email              = email;
          FirstName          = first_name;
          LastName           = last_name;
          Country            = country;
          CreateOn           = create_on;
          DeliveryDate       = delivery_date;
          OrderStatus        = order_status;
          ImageUrl           = image_url;
          LocalCreatedBy     = local_created_by;
          LocalCreatedAt     = local_created_at;
          LocalLastChangedBy = local_last_changed_by;
          LocalLastChangedAt = local_last_changed_at;
          LastChangedAt      = last_changed_at;
        }

    }

    define behavior for ZCDS_R_ITEM_0098 alias Item
    implementation in class zbp_r_item_0098 unique
    draft table zitem_0098_d
    persistent table zitem_0098
    lock dependent by _Header
    authorization dependent by _Header
    etag master LocalLastChangedAt
    {
      update ( precheck );
      delete ( precheck );

      association _Header { with draft; }

      field ( readonly, numbering : managed ) ItemUUID;
      field ( readonly ) HeaderUUID, LocalCreatedBy, LocalCreatedAt, LocalLastChangedBy, LocalLastChangedAt, LastChangedAt, ItemID;
      field ( mandatory ) Name, Description, Price, Currency, Quantity, ReleaseDate, DiscontinueDate, UnitOfMeasure;

      determination SetItemID on save { create; field ItemId; }
      determination SetDiscontinueDate on save { create; field DiscontinueDate; }

      action ( features : instance, authorization : update ) Discount parameter ZCDS_AE_Discount_0098 result [1] $self;

      validation ValidatePrice on save { create; update; field Price; }
      validation validateCurrency on save { create; update; field Currency; }
      validation ValidateQuantity on save { create; update; field Quantity; }

      determine action ValidatePrices { validation ValidatePrice; }
      determine action ValidateQuantitys { validation ValidateQuantity; }
      determine action ValidateCurrencys { validation validateCurrency; }

      side effects
      {
        determine action ValidatePrices executed on field Price affects messages;
        determine action ValidateQuantitys executed on field Quantity affects messages;
        determine action ValidateCurrencys executed on field Currency affects messages;
      }

      mapping for zitem_0098
        {
          ItemUUID           = id;
          HeaderUUID         = header_id;
          ItemID             = item_id;
          Name               = name;
          Description        = description;
          ReleaseDate        = release_date;
          DiscontinueDate    = discontinue_date;
          Price              = price;
          Currency           = currency;
          Height             = height;
          Width              = width;
          Depth              = depth;
          Quantity           = quantity;
          UnitOfMeasure      = unit_of_measure;
          LocalCreatedBy     = local_created_by;
          LocalCreatedAt     = local_created_at;
          LocalLastChangedBy = local_last_changed_by;
          LocalLastChangedAt = local_last_changed_at;
          LastChangedAt      = last_changed_at;
        }

    }